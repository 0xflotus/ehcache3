subprojects {
  apply plugin: 'java'
  apply plugin: 'maven-publish'

  group = 'org.ehcache.modules'
  version = '3.0.0-SNAPSHOT'

  archivesBaseName = "ehcache-${project.name}"

  sourceCompatibility = 1.6

  repositories {
    mavenCentral()
  }

  dependencies {
    testCompile 'junit:junit:4.11', 'org.hamcrest:hamcrest-library:1.3'
  }

  jar {
    manifest.attributes provider: 'gradle'
    archiveName = "ehcache-${baseName}-${version}.${extension}"
  }

  task sourceJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allJava
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
  }

  publishing {
    publications {
      mavenJava(MavenPublication) {

        artifactId archivesBaseName

        from components.java

        artifact sourceJar {
          classifier 'sources'
        }

        artifact javadocJar {
          classifier 'javadoc'
        }
      }
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.0'
}
