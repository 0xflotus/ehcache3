/*
 * Copyright Terracotta, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.gradle.internal.jvm.Jvm

// all these libs should be on server classpath for management
// management-entity-server  : entity used client-side to send management data into the server
// monitoring-service        : server monitoring service
// monitoring-service-entity : test entity, so that we can read the server monitoring service
// management-model          : management metadata describing exposed objects, stats and notifications (jdk-6 compat)
// cluster-topology          : model classes describing cluster topology (jdk-8 compat)
// management-registry       : service classes to expose management metadata and query api for stats (jdk-6 compat)
// sequence-generator        : improved boundary flake seq generator to add seq numbers on management messages
def serverCP = [
  'management-entity-server': ':plugin',
  'monitoring-service': ':plugin',
  'monitoring-service-entity': ':plugin',
  'management-model': '',
  'cluster-topology': '',
  'management-registry': '',
  'sequence-generator': '',
]

configurations {
  serverClasspath
}

dependencies {
  testCompile project(':dist')
  testCompile project(':clustered:clustered-dist')
  testCompile project(':management')
  testCompile "org.terracotta.management:management-entity-client:$parent.managementVersion"
  testCompile "org.terracotta.management:monitoring-service-entity:$parent.managementVersion"

  testCompile group:'org.terracotta', name:'galvan-support', version: galvanVersion
  testCompile (group:'com.google.code.tempus-fugit', name:'tempus-fugit', version:'1.1') {
    exclude group:'junit', module:'junit'
    exclude group:'org.hamcrest', module:'hamcrest-core'
  }
  testCompile group: 'javax.cache', name: 'cache-api', version: jcacheVersion

  serverCP.each { k, v ->
    serverClasspath "org.terracotta.management:$k:$parent.managementVersion$v"
  }
}

task unzipKit(type: Copy) {
  dependsOn project(':clustered:clustered-dist').distZip
  from zipTree(project(':clustered:clustered-dist').distZip.archivePath)
  into 'build/ehcache-kit'
}

compileTestJava {
  options.forkOptions.executable = Jvm.current().javacExecutable
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

test {
  dependsOn unzipKit
  executable = Jvm.current().javaExecutable
  // If you want to see all mutations of the voltron monitoring tree, add to JAVA_OPTS: -Dorg.terracotta.management.service.monitoring.VoltronMonitoringService.DEBUG=true
  environment 'JAVA_OPTS', '-Dcom.tc.l2.lockmanager.greedy.locks.enabled=false'
  //If this directory does not exist, tests will fail with a cryptic assert failure
  systemProperty 'kitInstallationPath', "$unzipKit.destinationDir/${project(':clustered:clustered-dist').archivesBaseName}-$project.version-kit"
  systemProperty 'managementPlugins', serverCP.keySet().collect { String artifact -> project.configurations.serverClasspath.find { it.name.startsWith("$artifact-$parent.managementVersion") } }.join(File.pathSeparator)
  // Uncomment to include client logging in console output
  // testLogging.showStandardStreams = true
}

